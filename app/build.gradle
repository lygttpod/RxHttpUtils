apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 24
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "com.allen.rxhttputils"
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 2
        versionName "2.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                // 输出apk名称为XXXX_v1.0_2016-01-15_wandoujia.apk
                def fileName = "RxHttpUtils_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {

        // 线上环境
        pro {
            buildConfigField "String", "BASE_URL", "\"https://api.douban.com/\""
        }

        // 上线前的预览版本
        releases {
            buildConfigField "String", "BASE_URL", "\"https://api.douban.com/\""
        }

        // 提测后（测试阶段)
        master {
            buildConfigField "String", "BASE_URL", "\"https://api.douban.com/\""
        }

        // 开发阶段
        dev {
            buildConfigField "String", "BASE_URL", "\"https://api.douban.com/\""
        }

    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [CHANNELID: name]
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    testCompile 'junit:junit:4.12'
    compile project(':library')
}
